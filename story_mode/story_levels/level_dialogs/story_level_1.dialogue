~ intro
Socks: Hiya, I'm Socks.
Socks: We've got a busy day ahead of us, starting with helping my person, Jenny.
- What does she need help with?
	Socks: She needs to get up and feed me! [wait=0.75] I mean go to work.
		We need to help her get up and get ready for work. 
- Okay, I can do that.
Socks: Do you know the basics of how stenography works?
- Of course
	=> try_walk
- Not really
	=> steno_basics


~ steno_basics
Socks: In stenography, you write words by hitting multiple keys at once. 
	Sounds at the start of the word or syllable are on the left.
	Vowel sounds are on the thumb keys.
	And sounds at the end of the word or syllable are on the right.
Socks: Are you familiar with the steno keyboard?
- Yes
- A bit
	=> offer_hints
- No
	=> offer_hints

~ try_walk
Socks: If you get stuck, you can always toggle hints for words on and off by hitting Enter (R-R), just in case.
Socks: Okay! We're going to start with words that have short vowels like 'cat', 'get', 'kid', 'top', and 'mud'.
Socks: Short vowels have two main rules: [wait=0.75]
	If you spell them with one letter, use that key to write them in a word. [wait=0.5]
	If you spell them with more than one letter, use the key that matches the sound they make.
do display_stroke("A")
Socks: We'll start with the short 'a' sound like in "cat".
Socks: Speaking of cats, have I mentioned how awesome I am?
Socks: Oh, be careful with words that have an 'r' - they can make the vowel sound a little funny. Look at the word 'car' [do display_stroke("KAR")] it doesn't sound like the 'a' in 'cat', but it's written with the A key!
Socks: Now, to got around here, all you need to do is write the word where you want to go, and I'll head over there. 
	Hit Enter or click anywhere to close my dialogue and try walking to one of the nearby points.
do hide_stroke()
do level.level_word_list.start_quest("first_steps")
=> END

~ offer_hints
Socks: Do you want to enable hints to show you how to stroke the words you'll find around the house?
- Yes
	do set_hints_visible(true)
- No
	do set_hints_visible(false)
=> try_walk

~ teach_interact
Socks: You've got me moving. Good start, but Jenny's a heavy sleeper. Walking around probably isn't going to do it.
Socks: If you reach a place where I can do something, a new word will appear in [bgcolor=BLACK][color={{PlayerConfig.interact_font_color}}]this color[/color][/bgcolor].
	[wait=0.2]Just stroke the word and I'll do the rest!
do level.level_word_list.start_quest("first_interact")
=> END

~ teach_gen_action
Socks: I forgot to tell you! I can do some things from [i]anywhere[/i]. 
	I can always meow, I'm very sociable! 
Socks: You can see my general actions and the words to use them in the bottom right.
Socks: Now you can help me wake Jenny up so that she's not late.
do level.level_word_list.start_quest("wake_up_jenny")
=> END

~ Jenny_woke_up
Socks: Oh good! She's [i]finally[/i] up. Now we can get out of here and see the rest of the house.
do level.level_word_list.start_quest("open_the_door")
=> END

~ meowed_at_door
if level.level_word_list.level_events["meowed_at_door_once"].event_complete == false:
	Socks: You might have to keep trying,
		Jenny's my favorite human, but...[wait=0.5]
		She's not always the best listener.
else: 
	Socks: I think she's coming!
=> END

~ jenny_opens_door
Jenny: Okay, okay. You can go out, Socks, but you know there isn't any food out there yet.
Socks: Did she say something about food? Let's go check my bowl!
do level.level_word_list.start_quest("check_bowl")
=> END


~ teach_o_vowel
do display_stroke("O")
Socks: Seems like you're getting the hang of things. Let's add something new.
	Short 'o' is the sound in 'cot', 'dog', or 'rod'. 
Socks: It's also the sound in 'core' or 'worn' - there's 'r' making things sound a little funny. 
Socks: You can include it in a word using the O key. Now get going - there's more to do!
do hide_stroke()
=> END

~ teach_e_vowel
do display_stroke("E")
Socks: Time for another vowel! Short 'e' is the sound in 'bed', 'men', and 'leg'. 
Socks: It's also the sound in 'her' or 'per'
do display_stroke("HER")
Socks: Be careful with the 'er' sound - it can also be spelled 'ir', like 'fir', or 'ur' like 'purr'. Remember the short vowel rules and use the key that matches the spelling. 
do hide_stroke()
=> END

~ teach_u_vowel
do display_stroke("U")
Socks: You know the drill by now, right? Our next vowel is short 'u', which you can hear in 'bus', 'fun', and 'tub'.
Socks: Remember what I told you about things that sound like 'er' - pay attention to the spelling. 'Her' is written with the E key, but 'fur' is written with U. 
do hide_stroke()
=> END

~ teach_i_vowel
Socks: Oh, you thought I was done? I have way more to teach you!
do display_stroke("EU")
Socks: There might not be any more vowel keys, but English definitely has more vowels.
	The last one for today is short 'i' - the sound in 'bit', 'tin', and 'miss'. 
Socks: This one uses two keys. Stroke E and U at the same time for the short vowel 'i'. 
do display_stroke("SEUR")
Socks: Remember that if you hear a short vowel spelled with one letter, you should use the letter that matches the spelling.
	That means 'sir' is written like this.
Socks: That's everything I have to teach you right now. You'd better hurry up and get Jenny off to work. Then we can meet my friends at the park. 
do hide_stroke()
do level.level_word_list.start_quest("get_jenny_out_the_door")
=> END

~ door_locked
Socks: Usually I can get doors open myself, but this one is pretty stuck. We're going to need Jenny's help to get it loosened up before I can open it. 
=> END

~ missing_headphones
Jenny: Socks, have you seen my headphones? They aren't by my bed where I left them...
Socks: Uh-oh... I might have played with those last night...
Socks: She'll never leave for work without those. Jenny always sticks them in when she leaves. I better find them.
do level.level_word_list.start_quest("find_jennys_headphones")
=> END

~ empty_bowl
Socks: This is unacceptable! Where's my breakfast? Every morning I get up and have my kibble before I go out.
Socks: Has Jenny forgotten me? Doesn't she realize that I need to eat? I guess I have to remind her.
do level.level_word_list.start_quest("eat_breakfast")
=> END


~ eating_breakfast
if level.level_word_list.level_events["breakfast_eaten"].event_complete == false:
	Socks: Kibble again. Delicious, but I'd much rather have some wet food. Or some fish, I do like fish...
	Socks: Well, I can always find some treats when I'm out later today. 
else:
	% Socks: I've already eaten breakfast. And Jenny is probably too smart to feed me twice... Let's get her out to work and then we can find someone to give me some more treats. 
	% Socks: All out of kibble...
=> END


~ end_level
Jenny: Bye Socks! Be good at home today, okay? No more hiding things.
Socks: Meow. \n[i]I don't just stay home all day, that would be boring[/i]
Jenny: Seriously, I've got a big day at work today, and am going out with Nick after. That's a lot of hours for you to get into something...Don't.
Socks: Purr \n[i]Stop worrying about me and have a good day, okay?[/i]
Jenny: I love you too.
do level.jenny_leave_home()
Socks: Well, she's all set, so now we can get going. Let's head out!
do level.finish_level()
=> END


~ sleepy_jenny
if level.level_word_list.quests["wake_up_jenny"].get_quest_progress(level.level_word_list.level_events) == 0:
	Jenny: Zzzzz...
elif level.level_word_list.quests["wake_up_jenny"].get_quest_progress(level.level_word_list.level_events) == 1:
	Jenny: Wha? I'm up...[wait = 0.3]
	Jenny: Zzzzz...
elif level.level_word_list.quests["wake_up_jenny"].get_quest_progress(level.level_word_list.level_events) == 2:
	Jenny: I'm up, I'm up! Can't you ever let me sleep in, Socks?
=> END


~ nothing_here
if level.level_word_list.level_events["headphones_revealed"].event_complete == false:
	Socks: Nope, nothing here. Hmmm...
=> END


~ sit_on_couch
Socks: Jenny and I cuddle here in the evening. It's pretty much the best part of the day. 
Socks: She always pets the good spot behind my ears. Sometimes I even get extra treats. 
if level.level_word_list.level_events["headphones_revealed"].event_complete == false:
	Socks: Oh yes, the headphones!
	=> nothing_here


~ headphones_found
Jenny: Oh, there they are. I must have dropped them when I got home. Weird...
Socks: Meow \n[i]Yep, that's definitely what happened...[/i]
=> END


~ leave_home
if level.level_word_list.active_quests.has("get_jenny_out_the_door"):
	Socks: I can't leave yet, Jenny's still here.
	% Socks: She needs to find her headphones for her walk.
	% [if level.level_word_list.active_quests.has("eat_breakfast")] Socks: And I haven't even had my breakfast!
	% Socks: She doesn't know I go out all day, but there's so much interesting stuff out there!

